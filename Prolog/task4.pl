:- encoding(utf8).

дорога(москва,     тверь,        170).
дорога(тверь,      санкт_петербург, 485).
дорога(москва,     владимир,     180).
дорога(владимир,   нижний_новгород, 230).
дорога(нижний_новгород, казань,  390).
дорога(казань,     уфа,          525).
дорога(уфа,        челябинск,    420).
дорога(челябинск,  екатеринбург, 210).
дорога(екатеринбург, тюмень,     325).
дорога(тюмень,     омск,         630).
дорога(омск,       новосибирск,  650).
дорога(новосибирск, красноярск,  780).
дорога(красноярск, иркутск,      1060).

соединён(A, B, D) :- дорога(A, B, D).
соединён(A, B, D) :- дорога(B, A, D).

путь_в_ширину(Старт, Финиш, Путь) :-
    поиск_в_ширину([[Старт]], Финиш, Обратный),
    обратить(Обратный, Путь).

поиск_в_ширину([[Финиш|Путь]|_], Финиш, [Финиш|Путь]).
поиск_в_ширину([[Текущий|Путь]|Остальные], Финиш, Результат) :-
    findall([Сосед,Текущий|Путь],
        (соединён(Текущий, Сосед, _),
         \+ содержит(Сосед, [Текущий|Путь])),
        Соседи),
    append(Остальные, Соседи, НоваяОчередь),
    поиск_в_ширину(НоваяОчередь, Финиш, Результат).
поиск_в_ширину([_|Остальные], Финиш, Результат) :-
    поиск_в_ширину(Остальные, Финиш, Результат).


путь_по_расстоянию(Старт, Финиш, Путь, Расстояние) :-
    дейкстра([0-[Старт]], Финиш, Расстояние-Обратный),
    обратить(Обратный, Путь).

дейкстра([Расст-[Финиш|Путь]|_], Финиш, Расст-[Финиш|Путь]).
дейкстра([Расст-Путь|Остальные], Финиш, Результат) :-
    Путь = [Текущий|_],
    findall(НовРасст-[Сосед|Путь],
        (соединён(Текущий, Сосед, D),
         \+ содержит(Сосед, Путь),
         НовРасст is Расст + D),
        Соседи),
    append(Остальные, Соседи, НоваяОчередь),
    keysort(НоваяОчередь, Отсортированная),
    дейкстра(Отсортированная, Финиш, Результат).

содержит(X, [X|_]).
содержит(X, [_|T]) :- содержит(X, T).

обратить(L, R) :- обр(L, [], R).
обр([], A, A).
обр([H|T], A, R) :- обр(T, [H|A], R).

написать_список([]).
написать_список([X]) :- write(X).
написать_список([H|T]) :-
    write(H), write(' → '),
    написать_список(T).


старт :-
    write('Введите начальный город (например: москва).'), nl,
    read(Старт),
    write('Введите конечный город (например: новосибирск).'), nl,
    read(Финиш), nl,

    write('1. Кратчайший путь по числу городов:'), nl,
    путь_в_ширину(Старт, Финиш, П1),
    написать_список(П1), nl,
    length(П1, L), Переездов is L - 1,
    write('Переездов: '), write(Переездов), nl, nl,

    write('2. Кратчайший путь по расстоянию:'), nl,
    путь_по_расстоянию(Старт, Финиш, П2, D),
    написать_список(П2), nl,
    write('Расстояние: '), write(D), write(' км'), nl.
